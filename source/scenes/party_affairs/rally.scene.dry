title: Political Rally
new-page: true
is-card: true
tags: party_affairs
view-if: rally_timer <= 0
on-arrival: month_actions += 1; rally_timer = 6
card-image: img/protest.jpg
go-to: main_rally if (sa_banned or sa_strength <= 180); sa_disrupt if (sa_strength > 180 and not sa_banned)

@sa_disrupt

The Nazi SA has disrupted our rally! Their street troops are preventing us from meeting.

- @police_protect: Call the police to protect our rally.
- @rb_protect: Use the Reichsbanner to protect our rally.
- @together_protect: Get the police and Reichsbanner to coordinate defense.
- @rfb_protect: Team up with KPD for the rally and its protection.
- @cancel: Cancel the rally.

@police_protect
view-if: spd_prussia == 1
on-arrival: police_protect_success = prussian_police_loyalty * prussian_police_militancy * prussian_police_strength*2 - sa_militancy*sa_strength;
go-to: police_protect_win if police_protect_success >= 0; police_protect_lose if police_protect_success < 0

@police_protect_win
go-to: main_rally

The police successfully protected our rally, so it can go ahead as usual.

@police_protect_lose
on-arrival: strife += 1; workers_nsdap += 3

Unfortunately, the police were unable or unwilling to hold back the SA mob. The Nazis have won this show of strength.

@together_protect
view-if: spd_prussia == 1 and police_rb_team = 1 and rb_banned = 0
on-arrival: together_protect_success = prussian_police_loyalty*prussian_police_militancy*prussian_police_strength*2 + rb_strength*rb_militancy - sa_militancy*sa_strength;
go-to: together_protect_win if together_protect_success >= 0; together_protect_lose if together_protect_success < 0

@together_protect_win
go-to: main_rally

The police and the Reichsbanner successfully protected our rally, so it can go ahead as usual.

@together_protect_lose
on-arrival: strife += 1; workers_nsdap += 3; 

Unfortunately, even the combined effort of the police and Reichsbanner was not enough to hold back the SA mob. The Nazis have won this show of strength.

@rb_protect
choose-if: rb_banned = 0
unavailable-subtitle: Reichsbanner is banned and cannot assemble.
on-arrival: rb_success = rb_strength*rb_militancy - sa_strength*sa_militancy; strife += 1
go-to: rb_protect_win if rb_success >= 0; rb_protect_lose if rb_success < 0

@rb_protect_win
go-to: main_rally

The Reichsbanner has successfully protected our rally against the SA! Our meeting can go head as usual.

@rb_protect_lose
on-arrival: workers_nsdap += 3; rb_strength -= 50

Unfortunately, the Reichsbanner was not able to hold back the SA mob. The Nazis have won this show of strength.

@rfb_protect
view-if: rfd_rb_team >= 1 and kpd_relation >= 55 and rb_banned = 0
on-arrival: rfb_success = rb_strength*rb_militancy + rfb_strength*rfb_militancy - sa_strength*sa_militancy; strife += 1
go-to: rfb_protect_win if rfb_success >= 0; rfb_protect_lose if rfb_success < 0

@rfb_protect_win
on-arrival: workers_kpd += 2; unemployed_kpd += 1; workers_spd += 4*(1-dissent); unemployed_spd += 3*(1-dissent) if left_strength > reformist_strength; workers_spd += 4*(1-dissent) if nationalization_progress > 0; unemployed_spd += 3*(1-dissent) if nationalization_progress > 0; left_strength += 5; left_dissent -= 5; old_middle_spd -= 3; new_middle_spd -= 2;

The Communists have helped the Reichsbanner to protect our rally against the SA! In return, we have agreed to focus our rally on the class struggle of the proletariat against the bourgeoisie. [? if nationalization_progress > 0 : Our achievements in nationalization have assisted our messaging. ?]

@rfb_protect_lose
on_arrival: kpd_relation -= 2; rfb_strength -= 30; rb_strength -= 30; workers_nsdap += 4

Unfortunately, even the combined effort of RFB and Reichsbanner was not enough to hold back the SA mob. The Nazis have won this show of strength.

@cancel
on-arrival: workers_nsdap += 3;

The SA have won this show of strength.

@main_rally

Mass rallies are a common feature of social democratic life, especially in the lead-up to an election. What is the primary theme of this rally?

- @class_struggle: The class struggle of the proletariat against the bourgeoisie.
- @welfare: Sozialpolitik - the maintenance and expansion of social welfare programs.
- @democracy: Expanding our nascent democracy and defending it against dictatorship and fascism.
- @pacifism: Opposing war, nationalism, and militarism at all costs.
- @wtb: Support the WTB plan!
- @local: A mixture of various local and national concerns that resonate most with the audience.
- @bashfash: Fighting the Nazis wherever we find them!
- @easy_discard

@class_struggle
on-arrival: workers_spd += 4*(1-dissent); unemployed_spd += 3*(1-dissent) if left_strength > reformist_strength; workers_spd += 4*(1-dissent) if nationalization_progress > 0; unemployed_spd += 3*(1-dissent) if nationalization_progress > 0; left_strength += 5; left_dissent -= 5; old_middle_spd -= 3; new_middle_spd -= 2; eat_the_rich += 1

Our message of class struggle appeals to the left wing of the working class, while potentially alienating the middle class. [? if nationalization_progress > 0 : Our achievements in nationalization have assisted our messaging. ?]

[? if eat_the_rich == 2: At one point, someone in the crowd yells "eat the rich". The conservative papers denounce it as a call to violence. ?]
[? if eat_the_rich == 3: Halfway through one of the speeches, the crowd starts chanting "eat the rich". The conservative papers denounce this inspired moment as proletarian barbarity. ?]
[? if eat_the_rich >= 4: As is traditional at this point, our speakers call upon the crowd to eat the rich. As always, conservative papers denounce us as degenerate cannibals. ?][? if rich_eaten: They are not completely wrong. ?]

@welfare
on-arrival: workers_spd += 3*(1-dissent); workers_spd += 3*(1-dissent) if welfare > 0; unemployed_spd += 3*(1-dissent) if welfare > 0; unemployed_spd += 4*(1-dissent) if welfare > 1; reformist_strength += 3; labor_strength += 4; reformist_dissent -= 3; labor_dissent -= 3;

The reformist message appeals to the organized workers, whose benefits are the accomplishments of social democracy. [? if welfare > 1 : Our achievements in expanding welfare have made this message more effective. ?]

@democracy
on-arrival: democratization += 1; pro_republic += 2*(1 - dissent); workers_spd += 4*(1-dissent) if democratization >= 3; unemployed_spd += 3*(1-dissent) if democratization >= 3; pro_republic += 4*(1 - dissent) if democratization >= 3; new_middle_spd += 4*(1-dissent) if democratization >= 3 

Our speakers defend the republican system and rail against the monarchists and authoritarians who threaten it.

[? if democratization < 3 : Talk of democracy does not excite the workers as much as more practical issues. But that may change; part of our job is to provide political education to the proletariat. ?]
[? if democratization >= 3 : Since we have consistently made democratization a theme of our campaign, the workers are enthusiastic about our message. ?]

@pacifism
on-arrival: pacifism += 1; nationalism -= 2*(1 - dissent); workers_spd += 4*(1 - dissent) if pacifism >= 3; unemployed_spd += 3*(1-dissent) if pacifism >= 3; nationalism -= 3*(1-dissent) if pacifism >= 3; new_middle_spd += 4*(1-dissent) if pacifism >= 3

Our speakers denounce the military establishment and all the spending entering that black hole. Some adventurous speakers question the responsibility of the military elites in organizing the Great War. [? if military_rearmed: Our commitment to the rearmament undercuts the effectiveness of our message. ?]

[? if pacifism < 3 : Given the volume of nationalist indoctrination in our society, it is difficult to argue so vehemently against the military. ?]
[? if pacifism >= 3 : Since we have consistently made pacifism a theme of our campaign, the workers are enthusiastic about our message. ?]

@wtb
view-if: wtb_adopted = 1
on-arrival: wtb_support += 1; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); rural_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent);

The WTB plan has become a popular rallying cry for our party. The workers are eager to go back to work, and that is exactly what the plan promises.


@local
on-arrival: workers_spd += 3*(1-dissent); old_middle_spd += 1*(1-dissent); new_middle_spd += 2*(1-dissent); unemployed_spd += 3*(1-dissent);

Our speakers have gauged the mood and are speaking to the issues that seem to most affect the workers in this moment.

@bashfash
view-if: (nazi_urgency > 1 or strife >= 3) and rb_banned = 0
on-arrival: nazi_urgency += 1; neorevisionist_strength += 8; neorevisionist_dissent -= 8; rb_strength += 20; spd_hostile += 1; workers_spd += 6*(1-dissent) if strife > 5; unemployed_spd += 3*(1-dissent) if strife > 5;

Our speakers urge the crowd to join the Reichsbanner and fight the Nazis by any and all means necessary.

title: Criminal Takeover
subtitle: Germany's economy falls to organized crime...
view-if: (unemployed >= 69 and nightmare = 0) or (unemployed >= 40 and nightmare >= 1)
new-page: true
max-visits: 1
on-arrival: unemployed_spd*= 0.6; unemployed_kpd += 5; unemployed_z*=0.8; unemployed_ddp*=0.8; unemployed_dvp*=0.8; unemployed_dnvp += 5; unemployed_nsdap += 12; pro_republic -= 15; criminal_takeover = 1; z_relation -= 8; dvp_relation -= 6; sa_strength += 150; prussian_police_strength*= 0.6
tags: event

= Criminal Takeover

Across Germany, tens of millions of people had been laid off with little recourse except to turn to crime. Large numbers of unemployed experts in manufacturing, finance, accounting, technology, law, and logistics have boosted the supply of world-class talent available to criminal cartels. Thanks to the weak economy, cash-rich criminal organizations acquired financially distressed but potentially valuable companies at bargain prices. Meanwhile, fiscal austerity demolished the budgets of law enforcement agencies and court systems.  The government has profoundly failed the country in the economy, education, health care, and other areas, and criminal syndicates are all too happy to fill the void in exchange for wealth, power, social legitimacy, and popular support. The far-right parties, who are accustomed to operating outside the law, are the largest political beneficiaries. 

What can we do?

- @crackdown
- @deal_with_it
- @looting
- @nothing

@crackdown
title: Encourage brutal police crackdowns.
unavailable-subtitle: [? if prussian_police_strength < 140 : If we increased the strength of the police, perhaps we could do this. ?] [? if prussian_police_loyalty < 0.7 : We need the obedience of the police first. ?]
choose-if: prussian_police_strength >= 140 and prussian_police_loyalty >= 0.7
on-arrival: kpd_relation -= 12; unemployed_spd -= 6; coalition_dissent -= 1; z_relation+= 4; dvp_relation += 4; unemployed_nsdap -= 8*(1-dissent); labor_dissent += 15; left_dissent += 15

If the thieves and looters are going to run wild, then we must restore order. The bourgeoisie approves of our bloody effort, even though it proves largely ineffectual. 


@deal_with_it
title: Make our own deals with the criminals.
choose-if: spd_total_corruption >= 4
unavailable-subtitle: We do not have sufficient connections among the underworld.
on-arrival: unemployed -= 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent);

While we cannot always outbid the fascists, we can at least offer some competition.


@looting
title: Join in the takeovers!
subtitle: Frankly, we should have done this long ago.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the organizational support of the works councils to pull this off. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and rb_militancy >= 0.25 and works_councils >= 1
on-arrival: nationalization_progress += 1; coup_progress += 5; unemployed -= 2; unemployed -= 1 if works_councils >= 3; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 4; workers_nsdap -= 8*(1-dissent); unemployed_nsdap -= 8*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; factory_takeovers += 1

We have empowered the workers to seize and take full control of the factories that have yet to fall to the syndicates, claiming that this is a protection measure. The bourgeoisie, of course, lumps us in with thieves and looters.


@nothing
title: There is nothing we can do.

As the country continues to burn around us, we conclude that there is nothing to be done.
